title: Intro
position: 289,-704
---
More portable vaporware?
Heck yeah!
This time on the playdate!
<<jump DemoSelect>>
===

title: DemoSelect
position: 178,-457
color: orange
---
<<once>>
Welcome to the demo select!
<<endonce>>

-> Node Group Test
    <<detour NodeGroupTest>>
-> Smart Variable Test
    <<detour SmartVariableTest>>
-> Text Layout Test
    <<detour TextLayout>>
-> Markup Test (wip)
    <<detour MarkupTest>>
-> Command Test (wip)
    <<detour CommandTest>>
-> Expression Test
    <<detour ExpressionTest>>    
<<jump DemoSelect>>
===

title: TextLayout
position: 859,-457
---
This is text that is very long and will wrap around to the next line. Can you even believe how long it?
===

title: MarkupTest
position: 863,-290
---
This is a test of markup
Midline pause for [pause=300 /] dramatic [pause /] effect.
Hey look! [plural value={1} one="A pie" other="Some pies" /]!
Hey look! [plural value={2} one="A pie" other="Some pies" /]!
I just baked [plural value={3} one="a pie" other="% pies" /]!
Selecting [select value={1} 1="option %" 2="option 2" /]!
Is this [bold]bold[/bold]?
Is this [italic]italic[/italic]?
someguy: Here's a character line. 
This has a tag #todo
This has a tag with a value #anothertag:3
This is a shadowed line #line:shadowtest #tagtest
This is a shadowed line #shadow:shadowtest #tagtest2
===

title: CommandTest
position: 863,-124
---
Let's see if commands work.
Press [italic]A[/italic] to wait for 5 seconds
<<wait  5>>
Did that work?
Now let's try some custom commands
(see console for output)
Static arguments test
<<test_command 1 2 3>>
Math arguments test
<<test_command {1 + 2} {3 * 4} {5 / 2}>>
Variable argument test
<<declare $test_variable = 42>>
<<test_command {$test_variable}>>
string arguments test
<<test_command "This is a test string" "Part 2">>
string argumentment with escape characters test
<<test_command "This is a \"test string\" " "with some" "'escape' characters">>
neat!
===
 
title: ExpressionTest
position: 864,54
---
1 + 2 = {1 + 2}
1 - 2 = {1 - 2}
1.5 ** 3 = {1.5 * 3}
4 / 2 = {4 / 2}
3 % 2 = {3 % 2}
1 > 2 = {1 > 2}
1 < 2 = {1 < 2}
1 >= 2 = {1 >= 2}
1 <= 2 = {1 <= 2}
1 is 4 = {1 is 4}
"test" eq "blag" = {"test" eq "blag"}
"a" + "b" = {"a" + "b"}
4 neq 5 = {4 neq 5}
true or false = {true or false}
true xor false = {true xor false}
not true = {not true }
true and false = {true and false}

===


title: SmartVariableTest
position: 862,-612
---
This is a test of smart variables.
<<declare $smartvarsimple = 1 + 2>>
<<declare $smartvarcomplex = 4 + $smartvarsimple>>
$smartvarsimple = 1 + 2 = {$smartvarsimple}.
$smartvarcomplex = 4 + $smartvarsimple = {$smartvarcomplex}.

===

title: NodeGroupTest
when: once
position: 36,-5
---
Once: Run this test a couple times to see different nodes in the node group be selected based on the saliency strategy.
===

title: NodeGroupTest
when: always
position: 299,-4
---
<<declare $nodeVarTest = 1>>
Always: After another visit, nodes with more complex conditions will start passing.
<<set $nodeVarTest to $nodeVarTest + 1>>
===

title: NodeGroupTest
when: $smartvarsimple > 2 
when: $nodeVarTest > 1
position: 299,172
---
<<declare $nodeVarTestA = 1>>
2 Conditions A: This has been seen {$nodeVarTestA} times
<<set $nodeVarTestA to $nodeVarTestA + 1>>
===

title: NodeGroupTest
when: $smartvarsimple > 2 
when: $nodeVarTest > 1
position: 41,175
---
<<declare $nodeVarTestB = 1>>
2 Conditions B: This has been seen {$nodeVarTestB} times
<<set $nodeVarTestB to $nodeVarTestB + 1>>
===